{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import numpy as np\n",
    "from robust_hermite_ft import hermite_function_basis as hfb"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -a\n",
    "\n",
    "# === Imports ===\n",
    "\n",
    "from libc.math cimport exp, sqrt, log\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "import cython\n",
    "\n",
    "# === Constants ===\n",
    "\n",
    "cdef double LOG_FOURTH_ROOT_OF_PI = -0.28618247146235  # ln(pi) / 4\n",
    "\n",
    "\n",
    "# === Interface ===\n",
    "\n",
    "def hermite_function_basis(\n",
    "    double[::1] x,\n",
    "    int n,\n",
    "    int workers,\n",
    "):\n",
    "    \"\"\"\n",
    "    Python wrapper for the evaluation of the Hermite function basis up to order ``n``\n",
    "    at the points ``x``.\n",
    "\n",
    "    Parameters\n",
    "    ----------\n",
    "    x : :class:`numpy.ndarray` of shape ``(num_x,)`` and dtype ``numpy.float64``\n",
    "        The points at which the Hermite function basis should be evaluated.\n",
    "    n : int\n",
    "        The order up to which the Hermite function basis should be evaluated.\n",
    "    workers : int\n",
    "        The number of workers to use for parallelisation.\n",
    "\n",
    "    Returns\n",
    "    -------\n",
    "    hermite_basis : :class:`numpy.ndarray` of shape ``(num_x, n + 1)`` and dtype ``numpy.float64``\n",
    "        The values of the Hermite functions.\n",
    "\n",
    "    \"\"\"\n",
    "\n",
    "    cdef int num_x = x.shape[0]\n",
    "    cdef double[::, ::1] hermite_basis = np.empty(\n",
    "        shape=(num_x, n + 1),\n",
    "        dtype=np.float64,\n",
    "    )\n",
    "\n",
    "    _eval_hermite_function_basis(\n",
    "        &x[0],\n",
    "        num_x,\n",
    "        n,\n",
    "        &hermite_basis[0, 0],\n",
    "        workers,\n",
    "    )\n",
    "\n",
    "    return np.asarray(hermite_basis)\n",
    "\n",
    "# === Auxiliary Functions ===\n",
    "\n",
    "cdef int _eval_hermite_function_basis(\n",
    "    double* x,\n",
    "    int num_x,\n",
    "    int n,\n",
    "    double* hermite_basis,\n",
    "    int workers,\n",
    "):\n",
    "    \"\"\"\n",
    "    Evaluates the Hermite function basis up to order ``n`` at the points ``x``.\n",
    "    It overwrites the input array ``hermite_basis`` with the results.\n",
    "\n",
    "    \"\"\"\n",
    "\n",
    "    # --- Variables ---\n",
    "\n",
    "    cdef int iter_x\n",
    "\n",
    "    # --- Main Loop ---\n",
    "\n",
    "    for iter_x in range(0, num_x):\n",
    "        _eval_hermite_function_basis_core(\n",
    "            x,\n",
    "            num_x,\n",
    "            n,\n",
    "            hermite_basis,\n",
    "            iter_x,\n",
    "        )\n",
    "\n",
    "    return 0  # dummy return value\n",
    "\n",
    "@cython.cdivision(True)\n",
    "cdef int _eval_hermite_function_basis_core(\n",
    "    double* x,\n",
    "    int num_x,\n",
    "    int n,\n",
    "    double* hermite_basis,\n",
    "    int iter_x,\n",
    "):\n",
    "    \"\"\"\n",
    "    Core function for the evaluation of the Hermite function basis.\n",
    "    For numerical stability, the Hermite functions are kept scaled to a value of 1\n",
    "    while the exponent of the correction factor phi is tracked separately to\n",
    "    compensate for this permanent re-scaling.\n",
    "\n",
    "    \"\"\"\n",
    "\n",
    "    # --- Variables ---\n",
    "\n",
    "    # to avoid the need for handling too many early exit cases, the virtual\n",
    "    # -1-th Hermite function h_{-1} is set to 0.0\n",
    "    # the 0-th Hermite function h_0 is (pi ** -0.25) * exp(-0.5 * x * x) but due to the\n",
    "    # correction scheme applied, it will be stored as 1 while the exponent of the\n",
    "    # correction factor phi is tracked separately\n",
    "    cdef int iter_j\n",
    "    cdef int base_index = iter_x * (n + 1)\n",
    "    cdef double iter_j_plus_1\n",
    "    cdef double xi = x[iter_x]\n",
    "    cdef double h_i_minus_1 = 0.0\n",
    "    cdef double h_i = 1.0\n",
    "    cdef double h_i_plus_1\n",
    "    cdef double exponent_correction = LOG_FOURTH_ROOT_OF_PI - 0.5 * xi * xi\n",
    "    cdef double prefactor_i_minus_1, prefactor_i\n",
    "    cdef double scale_factor\n",
    "\n",
    "    # --- Zero-th order ---\n",
    "\n",
    "    # for the zero-th order, the exponent correction just needs to be exponentiated\n",
    "    hermite_basis[base_index] = exp(exponent_correction)\n",
    "\n",
    "    # in case n == 0, the function evaluation is done\n",
    "    if n < 1:\n",
    "        return 0  # dummy return value\n",
    "\n",
    "    # --- Main Loop ---\n",
    "\n",
    "    # if higher orders are requested, a recursion is entered to compute the remaining\n",
    "    # Hermite functions\n",
    "    # the recursion is given by\n",
    "    # h_{i+1} = sqrt(2 / (i + 1)) * x * h_{i} - sqrt(i / (i + 1)) * h_{i-1}\n",
    "    # for a numerically stable calculation, the Hermite functions are kept scaled to a\n",
    "    # value of 1 while the exponent of the correction factor phi is tracked separately\n",
    "    # to compensate for this permanent re-scaling\n",
    "    # NOTE: the loop tackles all but the last iteration to avoid the need for a\n",
    "    #       conditional check in each iteration\n",
    "    base_index += 1\n",
    "    for iter_j in range(0, n - 1):\n",
    "        # first, the prefactors are calculated ...\n",
    "        # NOTE: the 1.0 has to be double to avoid integer division\n",
    "        iter_j_plus_1 = iter_j + 1.0\n",
    "        prefactor_i = sqrt(2.0 / iter_j_plus_1)\n",
    "        prefactor_i_minus_1 = sqrt(iter_j / iter_j_plus_1)\n",
    "\n",
    "        # ... followed by the actual recursion step ...\n",
    "        h_i_plus_1 = prefactor_i * xi * h_i - prefactor_i_minus_1 * h_i_minus_1\n",
    "        # ... after which the exponent correction is applied for storing the result\n",
    "        hermite_basis[base_index + iter_j] = exp(exponent_correction) * h_i_plus_1\n",
    "\n",
    "        # afterwards, the Hermite functions are scaled to a value of 1 and the exponent\n",
    "        # correction is updated to prepare for the next iteration\n",
    "        scale_factor = abs(h_i_plus_1) if h_i_plus_1 != 0.0 else 1.0\n",
    "        h_i_minus_1 = h_i / scale_factor\n",
    "        # NOTE: theoretically, h_{i+1} would be divided by its absolute value here,\n",
    "        #       but a / |a| = sign(a) so the expensive division can be stated as a\n",
    "        #       sign evaluation; here, everything relies on a sign definition that gives\n",
    "        #       0 for a value of 0 and not +1 or -1\n",
    "        if h_i_plus_1 != 0.0:\n",
    "            h_i = +1.0 if h_i_plus_1 > 0.0 else -1.0\n",
    "        else:\n",
    "            h_i = 0.0\n",
    "\n",
    "        exponent_correction += log(scale_factor)\n",
    "\n",
    "    # --- Last Iteration ---\n",
    "\n",
    "    # here, only the pre-factors and the recursion step are calculated\n",
    "    # NOTE: the 1.0 has to be double to avoid integer division\n",
    "    prefactor_i = sqrt(2.0 / n)\n",
    "    prefactor_i_minus_1 = sqrt((n - 1.0) / n)\n",
    "    hermite_basis[base_index + n - 1] = exp(exponent_correction) * (\n",
    "        prefactor_i * xi * h_i - prefactor_i_minus_1 * h_i_minus_1\n",
    "    )\n",
    "\n",
    "    return 0  # dummy return value\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = np.linspace(-10, 10, 3)\n",
    "n = 1_000\n",
    "\n",
    "test_one = hfb(x, n)\n",
    "test_two = hermite_function_basis(x, n, 1)\n",
    "print(test_one)\n",
    "print(\"\\n\")\n",
    "print(test_two)\n",
    "assert np.allclose(test_one, test_two)\n",
    "\n",
    "%timeit hfb(x, n, jit=True)\n",
    "%timeit hermite_function_basis(x, n, 1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "language_info": {
   "name": "python"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
